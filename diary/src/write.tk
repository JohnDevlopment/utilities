.nb add [ttk::frame .nb.frame1] -text Write -sticky news

ttk::label .nb.frame1.date

exw text -wrap word -scrolly .nb.frame1.text TEXTBOX -state disabled -undo 1
bind .nb.frame1.text.text <<Modified>> {
    set EntryModified [%W edit modified]
}
ttk::combobox .nb.frame1.entries -textvariable SELECTED_ENTRY -state readonly
set SELECTED_ENTRY ""
.nb.frame1.entries state disabled
bind .nb.frame1.entries <<ComboboxSelected>> {
    settemp date [%W get]
    set CurrentEntry [clock scan $date -format $DateFormat]
    exw subcmd .nb.frame1.text clear
    exw subcmd .nb.frame1.text insert 1.0 [dict get $FileData $CurrentEntry]
    exw subcmd .nb.frame1.text edit modified 0
    exw subcmd .nb.frame1.text edit reset
    selection clear -displayof .
    focus .nb.frame1.text.text
}

ttk::frame .nb.frame1.buttons

settemp scripts [list \
{
    exw state .nb.frame1.text normal
    exw subcmd .nb.frame1.text clear
    exw subcmd .nb.frame1.text edit modified 0
    exw subcmd .nb.frame1.text edit reset

    set CurrentEntry [clock seconds]
} \
{
    # Save entry

    if {[catch {expr "$CurrentEntry + 1"}]} {
        return [displayError "You are attempting to save an entry with no date" -title "Input Error"]
    }

    #if {[exw subcmd .nb.frame1.text instate disabled]} {
    if {[TEXTBOX instate disabled]} {
        return [displayError "The text field is disabled." -title "Input Error"]
    }

    settemp text [exw subcmd .nb.frame1.text get 1.0 end]

    if {$text ne ""} {
        dict set FileData $CurrentEntry [string trimright $text]

        # Reset modified flag and reset undo stack
        exw subcmd .nb.frame1.text edit modified 0
        exw subcmd .nb.frame1.text edit reset

        # File modified
        set FileModified 1
        printStatusbar 0 "Entry Saved" 2000
    } else {
        displayError "The text field is empty."
    }
} \
{
    # Delete entry

    if {$CurrentEntry eq ""} return

    settemp entry $CurrentEntry
    set CurrentEntry ""
    dict unset FileData $entry

    # File modified
    set FileModified 1
    printStatusbar 0 "Entry Deleted" 2000

    # Clear text; reset modified flag and reset undo stack
    exw subcmd .nb.frame1.text clear
    exw subcmd .nb.frame1.text edit modified 0
    exw subcmd .nb.frame1.text edit reset
}]
settemp w
settemp s
foreach w {new save delete} s $scripts {
    append s {
        if {[exw subcmd .nb.frame1.text instate normal]} {
            focus .nb.frame1.text.text
        } else {
            focus .nb.frame1
        }
    }

    ttk::button .nb.frame1.buttons.$w -text [string totitle $w] -command $s -state disabled
}

# Paned window
ttk::panedwindow .nb.frame1.statusbar -orient horizontal
.nb.frame1.statusbar add [ttk::label .nb.frame1.statusbar.entryMod -relief sunken] \
    -weight 1
.nb.frame1.statusbar add [ttk::label .nb.frame1.statusbar.fileMod -relief sunken] \
    -weight 1
.nb.frame1.statusbar add [ttk::label .nb.frame1.statusbar.dir -relief sunken] \
    -weight 2
after idle {printStatusbar 2 [pwd]}

pack .nb.frame1.date .nb.frame1.entries -fill x -side top -anchor n
exw pack .nb.frame1.text -before .nb.frame1.entries -fill both -side top -anchor n -expand 1

pack .nb.frame1.buttons -fill x -side top -anchor n
    settemp children [winfo children .nb.frame1.buttons]
    pack {*}$children -side left -fill x -expand 1 -anchor n

pack .nb.frame1.statusbar -fill x -side top
